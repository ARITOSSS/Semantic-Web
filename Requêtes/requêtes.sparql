# REQUETE GROUPE 3

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX schema: <https://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?country ?avgHeight ?purchaseCount
WHERE {
  {
    SELECT ?country (AVG(?height) AS ?avgHeight)
    WHERE {
      SERVICE <http://localhost:3030/nba/sparql> {
        ?player dbo:birthPlace/dbo:country ?country ;
                dcterms:temporal/dbo:height ?height .
      }
    }
    GROUP BY ?country
  }

  {
    SELECT ?country (COUNT(?purchase) AS ?purchaseCount)
    WHERE {
      SERVICE <http://localhost:3030/fraude/sparql> {
        ?purchase a schema:PurchaseAction ;
                  schema:location ?town .
        ?town dbo:country ?country .
      }
    }
    GROUP BY ?country
  }
}
ORDER BY DESC(?avgHeight)


#REQUETE GROUPE 3+12

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX schema: <https://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?country ?avgHeight ?purchaseCount ?currencies
WHERE {
  {
    SELECT ?country (AVG(?height) AS ?avgHeight)
    WHERE {
      SERVICE <http://localhost:3030/nba/sparql> {
        ?player dbo:birthPlace/dbo:country ?country ;
                dcterms:temporal/dbo:height ?height .
      }
    }
    GROUP BY ?country
  }

  {
    SELECT ?country (COUNT(?purchase) AS ?purchaseCount)
    WHERE {
      SERVICE <http://localhost:3030/fraude/sparql> {
        ?purchase a schema:PurchaseAction ;
                  schema:location ?town .
        ?town dbo:country ?country .
      }
    }
    GROUP BY ?country
  }

  {
    SELECT ?country (GROUP_CONCAT(DISTINCT ?currency; separator=", ") AS ?currencies)
    WHERE {
      SERVICE <http://localhost:3030/currency/sparql> {
        ?currency ^rdfs:label/dbo:usedInCountry ?country .
      }
    }
    GROUP BY ?country
  }
}
ORDER BY DESC(?purchaseCount)

#REQUETE REQUETE GROUPE 3+12+8

PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX schema: <https://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?country ?fraudCount ?avgHeight ?currencies ?athleteCount ?maleAthleteCount ((?maleAthleteCount / ?athleteCount) * 100 AS ?malePercentage)
WHERE {
  {
    SELECT ?country (AVG(?height) AS ?avgHeight)
    WHERE {
      SERVICE <http://localhost:3030/nba/sparql> {
        ?player dbo:birthPlace/dbo:country ?country ;
                dcterms:temporal/dbo:height ?height .
      }
    }
    GROUP BY ?country
  }

  {
    SELECT ?country (COUNT(?purchase) AS ?fraudCount)
    WHERE {
      SERVICE <http://localhost:3030/fraude/sparql> {
        ?purchase a schema:PurchaseAction ;
                  schema:location ?town .
        ?town dbo:country ?country .
      }
    }
    GROUP BY ?country
  }

  {
    SELECT ?country (GROUP_CONCAT(DISTINCT ?currency; separator=", ") AS ?currencies)
    WHERE {
      SERVICE <http://localhost:3030/currency/sparql> {
        ?currency ^rdfs:label/dbo:usedInCountry ?country .
      }
    }
    GROUP BY ?country
  }
  
  {
	SELECT ?country (COUNT(?person) AS ?athleteCount)
    WHERE{
      SERVICE <http://localhost:3030/athletes/sparql> {
        ?person dbo:nationality ?country ;
                dbo:gender ?gender
      }
    }
    GROUP BY ?country
  }
  
   {
    SELECT ?country (COUNT(?person) AS ?maleAthleteCount)
    WHERE {
      SERVICE <http://localhost:3030/athletes/sparql> {
        ?person dbo:nationality ?country ;
                dbo:gender ?gender .
        FILTER (?gender = dbr:Male)
      }
    }
    GROUP BY ?country
  }
  
}
ORDER BY DESC(?fraudCount)




#REQUETE GROUPE 8+12

PREFIX schemaTx:  <https://schema.org/>          # transactions(1).ttl
PREFIX dbo:       <http://dbpedia.org/ontology/>
PREFIX rdfs:      <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:       <http://www.w3.org/2001/XMLSchema#>
PREFIX pop:       <http://example.org/population/>   # world_population_with_requete_construct.ttl
PREFIX schemaPop: <http://schema.org/>
PREFIX sport:     <http://purl.org/sport/>       # athletes.ttl

SELECT ?countryNameTx ?continent
       (SUM(?amount) AS ?totalAmount)
       ?populationApprox
       (COUNT(DISTINCT ?ath) AS ?nbAthletes)
       ( SUM(?amount) / (?populationApprox / 1000000) AS ?amountPerMillionInhabitants )
WHERE {

  ## 1) Transactions dans /Transactions
  ?t a schemaTx:PurchaseAction ;
     schemaTx:amount ?amountLit ;
     dbo:country     ?dbpCountry .

  # Montant en décimal
  BIND(xsd:decimal(?amountLit) AS ?amount)

  # Nom du pays à partir de l'URI DBpedia
  #   ex: http://dbpedia.org/resource/United_Arab_Emirates
  #   -> "United Arab Emirates"
  BIND( STRAFTER(STR(?dbpCountry), "resource/") AS ?countryLocal )
  BIND( REPLACE(?countryLocal, "_", " ")          AS ?countryNameTxRaw )
  BIND( LCASE(?countryNameTxRaw)                  AS ?countryNameTx_lc )
  BIND( ?countryNameTxRaw                         AS ?countryNameTx )

  ## 2) Données de population via SERVICE world_population
  SERVICE <http://localhost:3030/world_population_with_requete_construct/sparql> {
    ?popCountry a pop:Country ;
                schemaPop:name      ?popName ;
                schemaPop:continent ?continent ;
                pop:areaKm2         ?areaKm2 ;
                pop:density         ?density .

    BIND( LCASE(STR(?popName)) AS ?countryNamePop_lc )
  }

  # Jointure texte pays (tolérante : égalité ou inclusion, pour gérer
  # "United States" / "United States of America", etc.)
  FILTER(
    ?countryNameTx_lc = ?countryNamePop_lc ||
    CONTAINS(?countryNameTx_lc, ?countryNamePop_lc) ||
    CONTAINS(?countryNamePop_lc, ?countryNameTx_lc)
  )

  # Population approx = surface × densité
  BIND( xsd:decimal(?areaKm2) * xsd:decimal(?density) AS ?populationApprox )

  ## 3) Athlètes via SERVICE athletes (optionnel)
  OPTIONAL {
    SERVICE <http://localhost:3030/athletes/sparql> {
      ?ath a sport:Athlete ;
           dbo:country ?dbpCountry .
    }
  }
}
GROUP BY ?countryNameTx ?continent ?populationApprox
HAVING (?populationApprox > 0)
ORDER BY DESC(?amountPerMillionInhabitants)
